import org.gradle.kotlin.dsl.*
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    java
    idea
    kotlin("jvm") version Versions.kotlin apply false
    jacoco
    id("io.gitlab.arturbosch.detekt") version Versions.detekt
}

buildscript {
    dependencies {
        classpath(Libs.grgitPlugin)
    }
}

fun RepositoryHandler.setup() {
    jcenter()
}

repositories.setup()

dependencies {
    detektPlugins(Libs.detektPlugin)
}

detekt {
    parallel = true
    input = files(subprojects.map { it.projectDir }.filter { it -> !it.name.contains("model") }, "buildSrc")
    config = files(rootDir.resolve("detekt-config.yml"))
}

/**
 * Folder with stored jacoco test coverage results
 */
val jacocoReportDir = "$buildDir${File.separator}${Consts.jacocoReportDir}"

val git: org.ajoberstar.grgit.Grgit? by lazy { runCatching { org.ajoberstar.grgit.Grgit.open(mapOf("currentDir" to project.rootDir)) }.getOrNull() }

val branchName: String = git?.branch?.current()?.name ?: ""

tasks {
    jacocoTestCoverageVerification {
        includeSubprojects()

        violationRules {
            rule { limit { minimum = Props.codeCoverageMinimum.get().toBigDecimal() } }
        }

        classDirectories.setFrom(
            sourceSets.main.get().output.asFileTree.matching {
                exclude(IgnoredFiles.jacoco)
            }
        )
    }

    check {
        dependsOn(
            detekt,
            jacocoTestCoverageVerification
        ) // fails when the code coverage is below value specified in Props.codeCoverageMinimum
    }

    jacocoTestReport {
        includeSubprojects()

        @Suppress("UnstableApiUsage") // Required for test coverage reports, however the api is still incubating
        reports {
            csv.isEnabled = false
            xml.isEnabled = true
            xml.destination = file(jacocoReportDir)
        }
    }

    withType<io.gitlab.arturbosch.detekt.Detekt> {
        exclude(IgnoredFiles.detekt) // exclude autogenerated packages
    }

    withType<KotlinCompile> {
        kotlinOptions {
            kotlinOptions.allWarningsAsErrors = false
        }
    }
}

tasks.test {
    @Suppress("UnstableApiUsage") // Required for running tests, however the api is still incubating
    useJUnitPlatform()

    testLogging {
        events("passed", "skipped", "failed")
    }

    systemProperty("spring.profiles.active", "test")
}

tasks.withType<KotlinCompile> {
    dependsOn(":backend:createVersionFile")

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xjvm-default=enable"
    }
}

//configurations {
//    all {
//        exclude("org.springframework.boot", "spring-boot-starter-logging")
////        exclude("ch.qos.logback", "logback-classic")
//    }
//}

subprojects {

    apply(plugin = "java")
    apply(plugin = "kotlin")
    apply(plugin = "jacoco")

    dependencies {
        implementation(Libs.kotlinStdlib)
        implementation(Libs.kotlinReflection)
        implementation(Libs.kotlinCoroutines)

        implementation(Libs.ktoolz)

        implementation(Libs.kotlinLogging) // logging DSL
        implementation(Libs.jackson) // json serializer

        // testing
        testImplementation(TestLibs.kotlinTest) // kotlin idiomatic testing
        testImplementation(TestLibs.mockk) // mock framework

        testImplementation(TestLibs.junitApi) // junit testing framework
        testImplementation(TestLibs.junitParams) // generated parameters for tests

        testRuntime(TestLibs.junitEngine) // testing runtime
    }

    configurations.all {
        resolutionStrategy {
            force(Libs.slf4j)
        }
    }

    tasks {
        @Suppress("UnstableApiUsage") // Required for running tests, however the api is still incubating
        withType<Test> {
            useJUnitPlatform()
            reports.junitXml.destination = file("${rootProject.buildDir}/test-results/${project.name}")
            jvmArgs("-Dspring.test.context.cache.maxSize=1")
            maxHeapSize = "2048M"
        }

        withType<KotlinCompile> {
            kotlinOptions.jvmTarget = "1.8"
            kotlinOptions.freeCompilerArgs += "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi" // coroutines support in web flux
        }

        withType<io.gitlab.arturbosch.detekt.Detekt> {
            exclude()
        }

        withType<KotlinCompile> {
            kotlinOptions {
                kotlinOptions.allWarningsAsErrors = false
            }

            sourceCompatibility = JavaVersion.VERSION_11.name
        }
    }

    group = rootProject.group
}

/**
 * Configures current [JacocoReportBase] in a way that it includes subprojects to its reports
 */
fun JacocoReportBase.includeSubprojects() {
    executionData.setFrom(fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec"))

    subprojects
        .forEach {
            this.sourceSets(it.sourceSets["main"])
            this.dependsOn(it.tasks["test"])
        }
}
