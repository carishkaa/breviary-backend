import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    java
    idea
    kotlin("jvm") version Versions.kotlin apply false
    jacoco
    id("io.gitlab.arturbosch.detekt") version Versions.detekt
}

fun RepositoryHandler.setup() {
    jcenter()
}

repositories.setup()

dependencies {
    detektPlugins(Libs.detektPlugin)
}

detekt {
    parallel = true
    input = files(subprojects.map { it.projectDir }.filterNot { it.name.contains("model") }, "buildSrc")
    config = files(rootDir.resolve("detekt-config.yml"))
}

/**
 * Folder with stored jacoco test coverage results
 */
val jacocoReportDir = "$buildDir${File.separator}${Consts.jacocoReportDir}"

tasks {
    jacocoTestCoverageVerification {
        includeSubprojects()

        violationRules {
            rule { limit { minimum = Props.codeCoverageMinimum.get().toBigDecimal() } }
        }

        classDirectories.setFrom(
            sourceSets.main.get().output.asFileTree.matching {
                exclude(IgnoredFiles.jacoco)
            }
        )
    }

    check {
        dependsOn(
            detekt,
            jacocoTestCoverageVerification
        ) // fails when the code coverage is below value specified in Props.codeCoverageMinimum
    }

    jacocoTestReport {
        includeSubprojects()

        @Suppress("UnstableApiUsage") // Required for test coverage reports, however the api is still incubating
        reports {
            csv.isEnabled = false
            xml.isEnabled = true
            xml.destination = file(jacocoReportDir)
        }
    }

    withType<io.gitlab.arturbosch.detekt.Detekt> {
        exclude(IgnoredFiles.detekt) // exclude autogenerated packages
    }

    withType<KotlinCompile> {
        kotlinOptions {
            kotlinOptions.allWarningsAsErrors = false
        }
    }

    test {
        @Suppress("UnstableApiUsage") // Required for running tests, however the api is still incubating
        useJUnitPlatform()

        testLogging {
            events("passed", "skipped", "failed")
        }

        systemProperty("spring.profiles.active", "test")
    }

    register("resolveDependencies") {
        doLast {
            project.allprojects.forEach { subProject ->
                with(subProject) {
                    buildscript.configurations.forEach { if (it.isCanBeResolved) it.resolve() }
                    configurations.compileClasspath.get().resolve()
                    configurations.testCompileClasspath.get().resolve()
                }
            }
        }
    }
}


subprojects {
    group = rootProject.group

    apply(plugin = "java")
    apply(plugin = "kotlin")
    apply(plugin = "jacoco")

    dependencies {
        implementation(Libs.kotlinStdlib)
        implementation(Libs.kotlinReflection)
        implementation(Libs.kotlinCoroutines)

        implementation(Libs.katlib)

        implementation(Libs.kotlinLogging) // logging DSL
        implementation(Libs.jackson) // json serializer

        // testing
        testImplementation(TestLibs.kotlinTest) // kotlin idiomatic testing
        testImplementation(TestLibs.mockk) // mock framework

        testImplementation(TestLibs.junitApi) // junit testing framework
        testImplementation(TestLibs.junitParams) // generated parameters for tests

        testRuntimeOnly(TestLibs.junitEngine) // testing runtime
    }

    configurations.all {
        resolutionStrategy {
            force(Libs.slf4j)
        }
    }

    tasks {
        @Suppress("UnstableApiUsage") // Required for running tests, however the api is still incubating
        withType<Test> {
            useJUnitPlatform()
            reports.junitXml.destination = file("${rootProject.buildDir}/test-results/${project.name}")
            jvmArgs("-Dspring.test.context.cache.maxSize=1")
            maxHeapSize = "2048M"
        }


        withType<io.gitlab.arturbosch.detekt.Detekt> {
            exclude()
        }

        withType<KotlinCompile> {
            kotlinOptions {
                allWarningsAsErrors = false
                jvmTarget = "1.8"
            }

            sourceCompatibility = JavaVersion.VERSION_1_8.name
        }
    }
}

/**
 * Configures current [JacocoReportBase] in a way that it includes subprojects to its reports
 */
fun JacocoReportBase.includeSubprojects() {
    executionData.setFrom(fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec"))

    subprojects
        .forEach {
            this.sourceSets(it.sourceSets["main"])
            this.dependsOn(it.tasks["test"])
        }
}
